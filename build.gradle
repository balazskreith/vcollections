plugins {
    id 'maven-publish'
    id 'java'
    id "com.jfrog.bintray" version "1.8.4"
}


allprojects {

    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = 1.12
    targetCompatibility = 1.12

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    group = 'com.wobserver.vcollections'

    task hello {
        doLast { task ->
            println "I'm $task.project.name "
        }
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
        implementation 'com.github.wobserver:vcollections:' + project.version
    }
}
subprojects {
    apply plugin: "maven-publish"

    hello {
        doLast {
            println "vcollectinos-${project.name}" + project.version
        }
    }

    ext.publish = true
    bintray {
        user = project.findProperty("bintray.user") ?: System.getenv("BINTRAY_USER")
        key = project.findProperty("bintray.key") ?: System.getenv("BINTRAY_API_KEY")
        publications = ['vcollections']
        pkg {
            repo = "vcollections"
            name = "vcollections-${project.name}"
            userOrg = 'wobserver'
            licenses = ['AGPL-V3']
            websiteUrl = 'https://github.com/wobserver/vcollections/'
            issueTrackerUrl = 'https://github.com/wobserver/vcollections/issues'
            vcsUrl = 'https://github.com/wobserver/vcollections.git'
            labels = ['collections', 'virtualization']
            publicDownloadNumbers = true
            version {
                name = project.version
                desc = project.desc
                vcsTag = project.version
            }
        }
    }

    task sourceJar(type: Jar) {
        classifier 'sources'
        from sourceSets.main.allJava
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/wobserver/vcollections")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USER")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_API_KEY")
                }
            }
        }
        publications {
            vcollections(MavenPublication) {
                from components.java
                groupId project.group
                artifactId "vcollections-${project.name}"
                artifact tasks.sourceJar
                version project.version
            }
            gpr(MavenPublication) {
                from components.java
                groupId project.group
                artifactId "vcollections-${project.name}"
                version project.version
            }
        }
    }
}